apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def DEFAULT_COMPILE_SDK_VERSION = 33
def DEFAULT_BUILD_TOOLS_VERSION = "33.0.0"
def DEFAULT_MIN_SDK_VERSION = 16
def DEFAULT_TARGET_SDK_VERSION = 33

def newArchEnabled = project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"

android {
    namespace "com.nozbe.watermelondb"

    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion rootProject.hasProperty('minSdkVersion') ? rootProject.minSdkVersion : DEFAULT_MIN_SDK_VERSION
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared", "-DREACT_NATIVE_NEW_ARCH_ENABLED=${newArchEnabled ? 1 : 0}"
                cppFlags '-std=c++17 -O2'
            }
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    packagingOptions {
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libfbjni.so'
        pickFirst '**/libsqlite3x.so'
        pickFirst '**/libzstd-jni-1.5.7-6.so'
        jniLibs {
            useLegacyPackaging true
        }
    }

    sourceSets {
        main {
            java.srcDirs += [
                "${project.buildDir}/generated/java",
                "${project.buildDir}/generated/jni"
            ]
        }
    }
}

if (newArchEnabled) {
    apply plugin: 'com.facebook.react'

    react {
        jsRootDir = file("../specs")
        libraryName = "WatermelonDBSpec"
        codegenJavaPackageName = "com.watermelondb.specs"
    }
}

dependencies {
    api("androidx.sqlite:sqlite-ktx:2.4.0")
    compileOnly files('libs/sqlite-android.aar')

    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'
    implementation "com.facebook.react:react-android"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation('io.socket:socket.io-client:2.1.1') {
        exclude group: 'org.json', module: 'json'
    }
    compileOnly files('libs/zstd-jni.aar')
    debugImplementation files('libs/sqlite-android.aar')
    debugImplementation files('libs/zstd-jni.aar')
    androidTestImplementation files('libs/sqlite-android.aar')
    androidTestImplementation files('libs/zstd-jni.aar')
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'

}

configurations.all {
    exclude group: "com.github.luben", module: "zstd-jni"
}
repositories {
    mavenCentral()

    flatDir{
        dirs 'libs'
    }
}
