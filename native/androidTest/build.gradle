// Top-level build file where you can add configuration options common to all sub-projects/modules.

import groovy.json.JsonSlurper

buildscript {
    ext.kotlin_version = '2.1.20'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

project.ext {
    compileSdkVersion = 34
    buildToolsVersion = '34.0.0'
    targetSdkVersion = 34
    minSdkVersion = 24
    reactNativeVersion = new JsonSlurper().parse(file("$rootDir/../../node_modules/react-native/package.json")).version
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../../node_modules/react-native/android"
        }
        maven {
            // Local Maven repo containing AARs with JSC library built for Android
            url "$rootDir/../../node_modules/jsc-android/dist"
        }
    }
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == "com.facebook.react" && details.requested.name == "react-native") {
                details.useTarget "com.facebook.react:react-android:${rootProject.ext.reactNativeVersion}"
            }
            if (details.requested.group == "com.facebook.react" && details.requested.name == "react-android" && !details.requested.version) {
                details.useVersion rootProject.ext.reactNativeVersion
            }
            if (details.requested.group == "com.facebook.react" && details.requested.name == "hermes-android" && !details.requested.version) {
                details.useVersion rootProject.ext.reactNativeVersion
            }
        }
        resolutionStrategy.force(
            "androidx.appcompat:appcompat:1.6.1",
            "androidx.appcompat:appcompat-resources:1.6.1",
            "androidx.core:core:1.12.0",
            "androidx.core:core-ktx:1.12.0",
            "androidx.sqlite:sqlite:2.3.1",
            "androidx.sqlite:sqlite-ktx:2.3.1",
            "androidx.annotation:annotation-experimental:1.3.1"
        )
    }
}
